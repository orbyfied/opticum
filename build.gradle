plugins {
    // java plugins
    id 'java'
    id 'java-library'
}

// Properties
group   'net.orbyfied.optium'
version '0.1.0'

/*
    Dependencies
 */

repositories {
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
}

project.ext.lwjglVersion = "3.3.1"

dependencies {
    api 'it.unimi.dsi:fastutil:8.5.8'

    api 'net.orbyfied.j8:j8-registry:0.1.0'
    api 'net.orbyfied.j8:j8-util:0.2.1.1_hotfix2'
    api 'net.orbyfied.j8:j8-event:0.1.0_hotfix1'

    // platforms
    // LWJGL
    compileOnlyApi platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    compileOnlyApi "org.lwjgl:lwjgl"
    compileOnlyApi "org.lwjgl:lwjgl-glfw"
    compileOnlyApi "org.lwjgl:lwjgl-jawt"
    compileOnlyApi "org.lwjgl:lwjgl-jemalloc"
    compileOnlyApi "org.lwjgl:lwjgl-stb"
}

/*
    Test Configuration
 */

dependencies {
    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

/*
    Generated Sources
 */

sourceSets {
    main {
        java {
            String grd = "${buildDir}/generated/main/java"
            project.ext.generatedResDir = grd
            srcDir(grd)
        }
    }
}

def fileMake(File f) {
    if (!f.exists()) {
        f.getParentFile().mkdirs();
        f.createNewFile();
    }
}

def callPyGenSourceFile(File file) {
    // create process builder with correct command and work directory
    ProcessBuilder b = new ProcessBuilder("py ", file.getName());
    b.directory(file.getParentFile())

    // set result directory
    Map<String, String> env = b.environment();
    env.put("RESULT_DIR", (String)project.ext.generatedResDir);

    // create log files
    File logDir = new File(buildDir, "/genlog/");
    String lfs  = file.toString().replace("\\", "_").replace("/", "_") + "+.txt";
    File out = new File(logDir, lfs.replace("+", "+out"));
    File err = new File(logDir, lfs.replace("+", "+err"));
    fileMake(out);
    fileMake(err);
    // redirect streams
    b.redirectOutput(out)
    b.redirectError(err)

    // start process
    Process p = b.start();
    int r = p.waitFor()

    // check return code
    if (r != 0) {
        throw new IllegalStateException((String)("Code generation failed, script: '" + file.getName() + "'\n" +
                "   Exit code: " + r + ", error log file: " + err.getAbsolutePath()));
    }
}

task("generate") {
    group 'build'
    doLast {
        // for all generation files
        File dir = new File("src/main/generated/");
        for (File file1 : dir.listFiles()) {
            if (!file1.name.endsWith(".py") || file1.name.startsWith("_"))
                continue;
            callPyGenSourceFile(file1);
        }
    }
}